! 'Chapman–Enskog soltions to arbitrary order in Sonine polynomials V: 
! Summational expressions for the viscosity-related bracket integrals'
! by R.V. Tompson, E.L. Tipton, S.K. Loyalka, European Journal of Mechanics B/Fluids 29 (2010) 153–179 
! the natural isotopic abundance averaged mass: m_He=4.002602d0;m_Ne=20.1797d0;
! m_Ar=39.948d0;m_Kr=83.80d0;m_Xe=131.29d0

#include"param.h"


SUBROUTINE ALPHA(T, MAXPQ, X, OMEGA11, OMEGA12, OMEGA22, D12, DT, LAMBDA)
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER P,Q,MAXPQ,I,J
  INTEGER I1,I2,IO1,IO2
  DOUBLE PRECISION, PARAMETER :: KB=1.380649D-23 ! BY DEF
  DOUBLE PRECISION, PARAMETER :: AMU=1.6605390666E-27 ! CODATA2018
  DOUBLE PRECISION,EXTERNAL :: FACT,POCH
  DOUBLE PRECISION,EXTERNAL ::  A
  DOUBLE PRECISION,INTENT(IN) :: T
  DOUBLE PRECISION,SAVE :: OLDT
  DOUBLE PRECISION,INTENT(IN) :: OMEGA11(MAXORD, MAXORD)
  DOUBLE PRECISION,INTENT(IN) :: OMEGA12(MAXORD, MAXORD)
  DOUBLE PRECISION,INTENT(IN) :: OMEGA22(MAXORD, MAXORD)
  DOUBLE PRECISION :: RAW_D_MAT(2*MAXPQ+1,2*MAXPQ+1)
  DOUBLE PRECISION :: D12(MAXPQ), DT(MAXPQ), LAMBDA(MAXPQ)
  DOUBLE PRECISION, ALLOCATABLE :: ALPHA_VEC(:),A_VEC(:)
  DOUBLE PRECISION, ALLOCATABLE :: A_MAT(:,:), A_INV(:,:)
  DOUBLE PRECISION, ALLOCATABLE :: DELTA_VEC(:),D_VEC(:)
  DOUBLE PRECISION, ALLOCATABLE :: D_MAT(:,:), D_INV(:,:)
  ! DOUBLE PRECISION :: ETA
  DOUBLE PRECISION :: X(2)
  DOUBLE PRECISION :: MASS(2)
  DOUBLE PRECISION :: M0,M1,M2
  DOUBLE PRECISION, EXTERNAL ::  BRACKETINTH
  DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
  DOUBLE PRECISION :: HINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
  COMMON /GASINFO/MASS ! 2 IS THE TEMP SOLUTION, WHICH CAN BE CHANGED
  COMMON /OMG/ GOMEGA, HINT
  INTEGER :: K

  M0=MASS(1)+MASS(2)
  M1=MASS(1)/M0
  M2=MASS(2)/M0

  GOMEGA(:,:,1,1)=OMEGA11
  GOMEGA(:,:,1,2)=OMEGA12
  GOMEGA(:,:,2,1)=OMEGA12 !
  GOMEGA(:,:,2,2)=OMEGA22

  IF(OLDT.NE.T) THEN ! Recalculate the integration when it's necessary
    DO IO2 = 1, 2
      DO IO1 = 1, 2
        DO I2 = 1, 2
          DO I1 = 1, 2
            DO Q = 0, MAXPQ
              DO P = 0, MAXPQ
                HINT(P,Q,I1,I2,IO1,IO2)=BRACKETINTH(P,Q,I1,I2,IO1,IO2)
              END DO
            END DO
          END DO
        END DO
      END DO
    END DO
    OLDT=T
  ENDIF

  RAW_D_MAT=0.0D0
  write(12,*) "X(1:2)", X(1:2)
  write(12,*) "MASS(1:2)", MASS(1:2)

  DO P=1,MAXPQ
    DO Q=1,MAXPQ
      ! WRITE(*,*) P, Q, X(1), X(2)
      RAW_D_MAT(MAXPQ+1+P,MAXPQ+1+Q)=A( P, Q, X(1), X(2)) ! SE
      RAW_D_MAT(MAXPQ+1+P,MAXPQ+1-Q)=A( P,-Q, X(1), X(2)) ! SW
      RAW_D_MAT(MAXPQ+1-P,MAXPQ+1-Q)=A(-P,-Q, X(1), X(2)) ! NW
      RAW_D_MAT(MAXPQ+1-P,MAXPQ+1+Q)=A(-P, Q, X(1), X(2)) ! NE
    ENDDO
  ENDDO

  DO P=1,MAXPQ
    RAW_D_MAT(MAXPQ+1+0, MAXPQ+1+P)=A( 0, P, X(1), X(2)) ! E
    RAW_D_MAT(MAXPQ+1+P, MAXPQ+1+0)=A( P, 0, X(1), X(2)) ! S
    RAW_D_MAT(MAXPQ+1+0, MAXPQ+1-P)=A( 0,-P, X(1), X(2)) ! W
    RAW_D_MAT(MAXPQ+1-P, MAXPQ+1+0)=A(-P, 0, X(1), X(2)) ! N
  enddo

  RAW_D_MAT(MAXPQ+1, MAXPQ+1)=A( 0, 0, X(1), X(2)) ! C



  DO I = 1, MAXPQ
    ALLOCATE(A_INV(2*I,2*I))
    ALLOCATE(A_MAT(2*I,2*I))
    ALLOCATE(ALPHA_VEC(2*I))
    ALLOCATE(A_VEC(2*I))

    A_MAT(I+1:I+I,I+1:I+I)=RAW_D_MAT(MAXPQ+1+1:MAXPQ+1+I ,MAXPQ+1+1:MAXPQ+1+I) ! SE
    A_MAT(I+1:I+I,  1:  I)=RAW_D_MAT(MAXPQ+1+1:MAXPQ+1+I ,MAXPQ+1-I:MAXPQ+1-1) ! SW
    A_MAT(  1:  I,  1:  I)=RAW_D_MAT(MAXPQ+1-I:MAXPQ+1-1 ,MAXPQ+1-I:MAXPQ+1-1) ! NW
    A_MAT(  1:  I,I+1:I+I)=RAW_D_MAT(MAXPQ+1-I:MAXPQ+1-1 ,MAXPQ+1+1:MAXPQ+1+I) ! NE

    WRITE(12,*) "A_MAT"
    DO k=1,2*I
      WRITE(12,'(1X,20G16.6)') (A_MAT(k,J),J=1,2*I) 
    ENDDO 

    CALL INVERSE(A_MAT,A_INV,2*I)

    WRITE(12,*) "A_INV"
    DO k=1,2*I
      WRITE(12,'(1X,20G16.6)') (A_INV(k,J),J=1,2*I) 
    ENDDO 

    ALPHA_VEC=0.0D0
    ALPHA_VEC(I)=-15.D0/4.D0*X(2)*DSQRT(2*KB*T/Mass(2)/AMU) ! ALPHA_-1
    ALPHA_VEC(I+1)=-15.D0/4.D0*X(1)*DSQRT(2*KB*T/Mass(1)/AMU) ! ALPHA_1

    WRITE(12,*) "ALPHA_VEC"
    WRITE(12,'(1X,20G16.6)') (ALPHA_VEC(J),J=1,2*I) 

    A_VEC=MATMUL(A_INV,ALPHA_VEC)

    LAMBDA(I) = -5.D0/4.D0*KB*DSQRT(2*KB*T/M0/AMU) * (X(1)/DSQRT(M1)*A_VEC(I+1) + X(2)/DSQRT(M2)*A_VEC(I))

    DEALLOCATE(A_INV)
    DEALLOCATE(A_MAT)
    DEALLOCATE(ALPHA_VEC)
    DEALLOCATE(A_VEC)

    ALLOCATE(D_INV(2*I+1,2*I+1))
    ALLOCATE(D_MAT(2*I+1,2*I+1))
    ALLOCATE(DELTA_VEC(2*I+1))
    ALLOCATE(D_VEC(2*I+1))

    D_MAT=RAW_D_MAT(MAXPQ+1-I:MAXPQ+1+I ,MAXPQ+1-I:MAXPQ+1+I) 

    WRITE(12,*) "D_MAT"
    DO k=1,2*I+1
      WRITE(12,'(1X,20G16.6)') (D_MAT(k,J),J=1,2*I+1) 
    ENDDO 

    CALL INVERSE(D_MAT,D_INV,2*I+1)

    WRITE(12,*) "D_INV"
    DO k=1,2*I+1
      WRITE(12,'(1X,20G16.6)') (D_INV(k,J),J=1,2*I+1) 
    ENDDO 

    DELTA_VEC=0.0D0
    DELTA_VEC(I+1)=3.D0/2.D0*DSQRT(2*KB*T/M0/AMU) ! DELTA_0

    WRITE(12,*) "DELTA_VEC"
    WRITE(12,'(1X,20G16.6)') (DELTA_VEC(J),J=1,2*I+1) 

    D_VEC=MATMUL(D_INV,DELTA_VEC)

    ! write(*,*) "M0", M0
    D12(I) =(KB*T)/1.013e5*   1.D0/2.D0*X(1)*X(2)*DSQRT(2*KB*T/M0/AMU) *D_VEC(I+1)
    DT(I) = (KB*T)/1.013e5*(-5.D0)/4.D0*X(1)*X(2)*DSQRT(2*KB*T/M0/AMU) *(X(1)/DSQRT(M1)*D_VEC(I+1+1) + X(2)/DSQRT(M2)*D_VEC(I+1-1))

    DEALLOCATE(D_INV)
    DEALLOCATE(D_MAT)
    DEALLOCATE(DELTA_VEC)
    DEALLOCATE(D_VEC)

  enddo

  RETURN
END SUBROUTINE ALPHA

DOUBLE PRECISION FUNCTION BRACKETINTH(P,Q,I1,I2,OI1,OI2)
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P, Q, I1, I2, OI1, OI2
  DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
  DOUBLE PRECISION :: HINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
  DOUBLE PRECISION :: OMEGA (MAXORD, MAXORD)
  DOUBLE PRECISION, EXTERNAL:: H12, H1, HSG
  COMMON /OMG/ GOMEGA,HINT
  IF (OI1.LE.OI2) THEN
    OMEGA=GOMEGA(:,:,OI1,OI2)
  ELSE
    OMEGA=GOMEGA(:,:,OI2,OI1)
  ENDIF
  ! WRITE(*,*) "OI1, OI2, OMEGA(2,2)"
  ! WRITE(*,*) OI1, OI2, OMEGA(2,2)
  ! I AM A LITTLE LAZY HERE FOR FORTRAN DOES NOT HAVE DICT
  IF(I1.NE.I2) THEN
    BRACKETINTH=H12(P,Q,I1,I2,OMEGA)
  ELSE
    IF(OI1.NE.OI2) THEN
      BRACKETINTH=H1(P,Q,I1,I2,OMEGA)
    ELSE
      BRACKETINTH=HSG(P,Q,I1,I2,OMEGA)
    ENDIF
  ENDIF
  RETURN
END FUNCTION BRACKETINTH

DOUBLE PRECISION FUNCTION H12(P,Q,I1,I2,OMEGA) ! EQ. 109
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  DOUBLE PRECISION :: M0, M1, M2
  DOUBLE PRECISION :: MASS(2)
  COMMON /GASINFO/ MASS
  INTEGER :: L,R
  DOUBLE PRECISION S_SUM
  M0=MASS(I1)+MASS(I2)
  M1=MASS(I1)/M0
  M2=MASS(I2)/M0
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=1,MIN(P,Q)+1
    DO R=L,(P+Q+2-L)
      ! WRITE(*,*) "B''*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      S_SUM=S_SUM+A(L,R)*OMEGA(L,R)
    ENDDO
  ENDDO
  H12=S_SUM*8
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION A(L,R) ! EQ 110
    ! NOT THAT B FUNCTION AUTOMATICALLY HAS THE VARIABLES FROM L12,
    ! WHICH, MAKES THE PROAGRAM NEAT
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    INTEGER :: I
    DOUBLE PRECISION, EXTERNAL:: FACT, DELTA
    DOUBLE PRECISION A_SUM

    A_SUM=0.0D0
    A=0.0D0
    !!! REMENBER THAT 1-DELTA=0 CAUSE ZERO VALUE
    DO I=L-1,MIN(P,Q,R,(P+Q+1-R))
      A_SUM=A_SUM+8.0**DBLE(I)*FACT(P+Q-2*I)/(FACT(P-I)*FACT(Q-I)) &
        &*(-1.0)**DBLE(L)/(FACT(L)*FACT(I+1-L)) &
        &*(-1.0)**DBLE(R+I)/(FACT(R-I)*(FACT(P+Q+1-I-R))) &
        &* FACT(R+1)/FACT(2*R+2) &
        &* FACT(2*(P+Q+2-I))/FACT(P+Q+2-I) &
        &* 2.0**DBLE(2*R)/(4.0**DBLE(P+Q+1)) & 
        &* (DBLE(I+1-L)*DBLE(P+Q+1-I-R)-DBLE(L*(R-I)) )
    ENDDO

    A=A_SUM*M2**(P+0.5D0)*M1**(Q+0.5)
    RETURN
  END FUNCTION A
END FUNCTION H12


DOUBLE PRECISION FUNCTION H1(P,Q,I1,I2,OMEGA)  ! EQ. 111
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  INTEGER :: L,R
  DOUBLE PRECISION G,F, M0,M1,M2, S_SUM
  DOUBLE PRECISION :: MASS(2)
  COMMON /GASINFO/  MASS
  M0 = I2 ! SUPPRESS WARNING WARNING
  ! NOTE HERE THAT ...
  ! WE ALWAYS HAVE M0 THE SUM OF ATOMIC MASSES
  ! M1 FOR THE ATOM "TREATED"
  ! AND M2 THE OTHER ATOM
  M0=MASS(1)+MASS(2) 
  M1=MASS(I1)/M0
  M2=MASS(3-I1)/M0 ! A TEMP SOLUTION FOR BIANRY MIXTURE
  G=(M1-M2)/M2
  F=(M1**2+M2**2)/(2*M1*M2)
  ! WRITE(*,*) ">>>L1<<<, M0, M1, M2, G, F"
  ! WRITE(*,*) "        ", M0, M1, M2, G, F
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=1,MIN(P,Q)+1
    DO R=L,(P+Q+2-L)
      ! WRITE(*,*) "B'*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      !!! EXTERNAL OMEGA IS MISSING
      S_SUM=S_SUM+A(L,R)*OMEGA(L,R)
      !S_SUM=S_SUM+B
    ENDDO
  ENDDO
  H1=S_SUM*8
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION A(L,R) ! EQ. 112
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    INTEGER I,W
    DOUBLE PRECISION A_SUM,A_SUM1,A_SUM2
    DOUBLE PRECISION, EXTERNAL :: DELTA, FACT, POCH


    A=0.0D0
    A_SUM=0.0D0
    A_SUM1=0.0D0
    DO I=L-1,MIN(P,Q,R,(P+Q+1-R))
      A_SUM1=8.0**DBLE(I)*FACT(P+Q-2*I)/(FACT(P-I)*FACT(Q-I))&
        &*1.0/(FACT(L)*FACT(I+1-L)) &
        &*(-1.0)**DBLE(R+I)/(FACT(R-I)*FACT(P+Q+1-I-R))&
        &*FACT(R+1)/FACT(2*R+2) &
        &*FACT(2*(P+Q+2-I))/FACT(P+Q+2-I)&
        &*2.0**DBLE(2*R)/4.0**DBLE(P+Q+1)
      A_SUM2=0.0D0
      DO W=0,MIN(P,Q,P+Q+1-R)-I
        A_SUM2=A_SUM2+F**DBLE(I+1-L)*G**DBLE(W)/FACT(W) & 
          &*POCH(P+Q+2-I-R-W,W)*POCH(P+1-I-W,W) & 
          &/POCH(2*(P+Q+2-I)-2*W+1,W) & 
          &*POCH(P+Q+3-I-W,W)*POCH(Q+1-I-W,W) & 
          &/POCH(2*(P+Q+2-I)-W+1,W) &
          &*2.0**DBLE(2*W-1)*M1**(I)*M2**(I)*M2**(P+Q-2*I-W) & 
          &/POCH(P+Q+1-2*I-W,W)&
          &*(2.0*M1**(1)/F*DBLE((I+1-L)*(P+Q+1-I-R-W))-2.0*M2**(1)*DBLE(L*(R-I)))
      ENDDO
      A_SUM=A_SUM+A_SUM1*A_SUM2
    ENDDO

    A=A_SUM
    RETURN 
  END FUNCTION A
END FUNCTION


DOUBLE PRECISION FUNCTION HSG(P,Q,I1,I2,OMEGA) ! EQ 113
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  INTEGER :: L,R
  DOUBLE PRECISION S_SUM
  IF (I1.NE.I2) THEN
    WRITE(*,*) "HERE"
    STOP
  ENDIF
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=2,MIN(P,Q)+2    ! L=1, MIN(P,Q)+2
    DO R=L,(P+Q+4-L)
      ! WRITE(*,*) "B'''*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      !!! EXTERNAL OMEGA IS MISSING
      S_SUM=S_SUM+A(L,R)*OMEGA(L,R)
      !S_SUM=S_SUM+B
    ENDDO
  ENDDO
  HSG=S_SUM*8
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION A(L,R) !EQ 114
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    DOUBLE PRECISION, EXTERNAL :: FACT, POCH, DELTA
    INTEGER I
    DOUBLE PRECISION A_SUM

    A=0.0D0
    A_SUM=0.0D0
    DO I=L-1,MIN(P,Q,R,(P+Q+1-R))
      A_SUM=A_SUM+8.0**DBLE(I)*FACT(P+Q-2*I)/(FACT(P-I)*FACT(Q-I))&
        &*(1.0+(-1.0)**DBLE(L))/(FACT(L)*FACT(I+1-L))&
        &*(-1.0)**DBLE(R+I)/(FACT(R-I)*FACT(P+Q+1-I-R)) &
        &*FACT(R+1)/FACT(2*R+2) &
        &*FACT(2*(P+Q+2-I))/FACT(P+Q+2-I)&
        &*2.0**DBLE(2*R)/4.0**DBLE(P+Q+1) &
        &*( DBLE((I+1-L)*(P+Q+1-I-R))-DBLE(L*(R-I)) &
        &)
    ENDDO

    A=A_SUM*0.5**DBLE(P+Q+1)

  END FUNCTION A

END FUNCTION

DOUBLE PRECISION FUNCTION A(P,Q,X1,X2) ! FOR EQ 5
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: P, Q
  DOUBLE PRECISION, INTENT(IN) :: X1, X2
  A=AA(P,Q)
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION AA(P,Q) ! EQS. 7 8 9 10 
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: P, Q
    DOUBLE PRECISION, EXTERNAL :: BRACKETINTL
    INTEGER :: AP, AQ
    ! INTEGER, PARAMETER :: MAXORD=20
    DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
    DOUBLE PRECISION :: HINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
    DOUBLE PRECISION :: M0, M1, M2
    DOUBLE PRECISION :: MASS(2)
    COMMON /GASINFO/ MASS
    COMMON /OMG/ GOMEGA,HINT
    M0=MASS(1)+MASS(2)
    M1=MASS(1)/M0
    M2=MASS(2)/M0
    AA=0.D0
    AP=ABS(P)
    AQ=ABS(Q)
    IF (P.GT.0 .AND. Q.GT.0) THEN
      AA=X1**2*HINT(AP,AQ,1,1,1,1) &
        & +X1*X2*HINT(AP,AQ,1,1,1,2)
    ELSEIF (P.GT.0 .AND. Q.LT.0) THEN
      AA=X1*X2*HINT(AP,AQ,1,2,1,2)
    ELSEIF (P.LT.0 .AND. Q.GT.0) THEN
      AA=X1*X2*HINT(AP,AQ,2,1,2,1)
    ELSEIF (P.LT.0 .AND. Q.LT.0) THEN
      AA=X2**2*HINT(AP,AQ,2,2,2,2)&
        &+X1*X2*HINT(AP,AQ,2,2,2,1)
    ELSEIF (P.GT.0 .AND. Q.EQ.0) THEN
      AA=X1*X2*M1**0.5*HINT(AP,0,1,1,1,2)
    ELSEIF (P.EQ.0 .AND. Q.GT.0) THEN
      AA=X1*X2*M1**0.5*HINT(AQ,0,1,1,1,2)
    ELSEIF (P.LT.0 .AND. Q.EQ.0) THEN
      AA=-X1*X2*M2**0.5*HINT(AP,0,2,2,2,1)
    ELSEIF (P.EQ.0 .AND. Q.LT.0) THEN
      AA=-X1*X2*M2**0.5*HINT(AQ,0,2,2,2,1)
    ELSEIF (P.EQ.0 .AND. Q.EQ.0) THEN
      AA=X1*X2*(8*M1*M2*GOMEGA(1,1,1,2))
    ENDIF
    RETURN
  END FUNCTION AA
END FUNCTION A


