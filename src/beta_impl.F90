! 'Chapman–Enskog soltions to arbitrary order in Sonine polynomials V: 
! Summational expressions for the viscosity-related bracket integrals'
! by R.V. Tompson, E.L. Tipton, S.K. Loyalka, European Journal of Mechanics B/Fluids 29 (2010) 153–179 
! the natural isotopic abundance averaged mass: m_He=4.002602d0;m_Ne=20.1797d0;
! m_Ar=39.948d0;m_Kr=83.80d0;m_Xe=131.29d0

#include"param.h"


SUBROUTINE BETA(T, MAXPQ, X, OMEGA11, OMEGA12, OMEGA22, ETA)
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER P,Q,MAXPQ,I
  INTEGER I1,I2,IO1,IO2
  DOUBLE PRECISION, PARAMETER :: KB=1.380649D-23 ! BY DEF
  DOUBLE PRECISION,EXTERNAL :: FACT,POCH
  DOUBLE PRECISION,EXTERNAL ::  B
  DOUBLE PRECISION,INTENT(IN) :: T
  DOUBLE PRECISION,SAVE :: OLDT
  DOUBLE PRECISION,INTENT(IN) :: OMEGA11(MAXORD, MAXORD)
  DOUBLE PRECISION,INTENT(IN) :: OMEGA12(MAXORD, MAXORD)
  DOUBLE PRECISION,INTENT(IN) :: OMEGA22(MAXORD, MAXORD)
  DOUBLE PRECISION :: RAW_B_MAT(2*MAXPQ,2*MAXPQ)
  DOUBLE PRECISION :: ETA(MAXPQ)
  DOUBLE PRECISION, ALLOCATABLE :: BETA_VEC(:),B_VEC(:)
  DOUBLE PRECISION, ALLOCATABLE :: B_MAT(:,:), B_INV(:,:)
  ! DOUBLE PRECISION :: ETA
  DOUBLE PRECISION :: X(2)
  DOUBLE PRECISION :: MASS(2)
  DOUBLE PRECISION, EXTERNAL ::  BRACKETINTL
  DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
  DOUBLE PRECISION :: LINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
  COMMON /GASINFO/MASS ! 2 IS THE TEMP SOLUTION, WHICH CAN BE CHANGED
  COMMON /OMG/ GOMEGA, LINT
  character(10) :: fmtstr

  GOMEGA(:,:,1,1)=OMEGA11
  GOMEGA(:,:,1,2)=OMEGA12
  GOMEGA(:,:,2,1)=OMEGA12 !
  GOMEGA(:,:,2,2)=OMEGA22

  IF(OLDT.NE.T) THEN ! Recalculate the integration when it's necessary
    DO IO2 = 1, 2
      DO IO1 = 1, 2
        DO I2 = 1, 2
          DO I1 = 1, 2
            DO Q = 0, MAXPQ
              DO P = 0, MAXPQ
                LINT(P,Q,I1,I2,IO1,IO2)=BRACKETINTL(P,Q,I1,I2,IO1,IO2)
              END DO
            END DO
          END DO
        END DO
      END DO
    END DO
    OLDT=T
  ENDIF

  RAW_B_MAT=0.0D0
  write(12,*) "X(1:2)", X(1:2)
  write(12,*) "MASS(1:2)", MASS(1:2)

  DO P=1,MAXPQ
    DO Q=1,MAXPQ
      ! WRITE(*,*) P, Q, X(1), X(2)
      RAW_B_MAT(  MAXPQ+P,  MAXPQ+Q)=B( P, Q, X(1), X(2)) ! SE
      RAW_B_MAT(  MAXPQ+P,MAXPQ+1-Q)=B( P,-Q, X(1), X(2)) ! SW
      RAW_B_MAT(MAXPQ+1-P,MAXPQ+1-Q)=B(-P,-Q, X(1), X(2)) ! NW
      RAW_B_MAT(MAXPQ+1-P,  MAXPQ+Q)=B(-P, Q, X(1), X(2)) ! NE
    ENDDO
  ENDDO

  ! WRITE(*,*) "B_MAT"
  ! DO I=1,2*MAXPQ
    ! WRITE(12,'(1X,10G16.6)') (B_MAT(I,J),J=1,2*MAXPQ) 
  ! ENDDO 


  DO I = 1, MAXPQ
    ALLOCATE(B_INV(2*I,2*I))
    ALLOCATE(B_MAT(2*I,2*I))
    ALLOCATE(BETA_VEC(2*I))
    ALLOCATE(B_VEC(2*I))
    B_MAT=RAW_B_MAT(MAXPQ-I+1:MAXPQ+I, MAXPQ-I+1:MAXPQ+I)
    ! A TEMP MAT FOR COMPUTATION BECAUSE THE INVERSION CODE WE USE
    ! CAN DESTROY THE MATRIX
    CALL INVERSE(B_MAT,B_INV,2*i)

    BETA_VEC=0.0D0
    BETA_VEC(I)=5.D0/2.D0*X(2) ! BETA_-1
    BETA_VEC(I+1)=5.D0/2.D0*X(1) ! BETA_1

    B_VEC=MATMUL(B_INV,BETA_VEC)


    ETA(I) = (X(1)*B_VEC(I+1) + X(2)*B_VEC(I))*T*KB ! SI UNIT
    WRITE(12,*) "T = ", T,  ", VISCOSITY OF ORDER ",I, " = ", ETA(I)  * 1.D6
    DEALLOCATE(B_INV)
    DEALLOCATE(B_MAT)
    DEALLOCATE(BETA_VEC)
    DEALLOCATE(B_VEC)
  enddo

  write(fmtstr,'(i0)') MAXPQ

  WRITE(12,"(A6)", advance="no") 'T[K]'
  do i = 1,MAXPQ
    WRITE(12,"("//trim(fmtstr)//"I20)",advance='no') i
  enddo
  WRITE(12,*) 

  WRITE(12,"(F6.2)", advance="no") T
  WRITE(12,"("//trim(fmtstr)//"F20.8)") ETA * 1.D6

  RETURN
END SUBROUTINE BETA

DOUBLE PRECISION FUNCTION BRACKETINTL(P,Q,I1,I2,OI1,OI2)
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P, Q, I1, I2, OI1, OI2
  DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
  DOUBLE PRECISION :: LINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
  DOUBLE PRECISION :: OMEGA (MAXORD, MAXORD)
  DOUBLE PRECISION, EXTERNAL:: L12, L1, LSG
  COMMON /OMG/ GOMEGA,LINT
  IF (OI1.LE.OI2) THEN
    OMEGA=GOMEGA(:,:,OI1,OI2)
  ELSE
    OMEGA=GOMEGA(:,:,OI2,OI1)
  ENDIF
  ! WRITE(*,*) "OI1, OI2, OMEGA(2,2)"
  ! WRITE(*,*) OI1, OI2, OMEGA(2,2)
  ! I AM A LITTLE LAZY HERE FOR FORTRAN DOES NOT HAVE DICT
  IF(I1.NE.I2) THEN
    BRACKETINTL=L12(P,Q,I1,I2,OMEGA)
  ELSE
    IF(OI1.NE.OI2) THEN
      BRACKETINTL=L1(P,Q,I1,I2,OMEGA)
    ELSE
      BRACKETINTL=LSG(P,Q,I1,I2,OMEGA)
    ENDIF
  ENDIF
  RETURN
END FUNCTION BRACKETINTL

DOUBLE PRECISION FUNCTION L12(P,Q,I1,I2,OMEGA) ! EQ. 109
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  DOUBLE PRECISION :: M0, M1, M2
  DOUBLE PRECISION :: MASS(2)
  COMMON /GASINFO/ MASS
  INTEGER :: L,R
  DOUBLE PRECISION S_SUM
  M0=MASS(I1)+MASS(I2)
  M1=MASS(I1)/M0
  M2=MASS(I2)/M0
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=1,MIN(P,Q)+2
    DO R=L,(P+Q+4-L)
      ! WRITE(*,*) "B''*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      S_SUM=S_SUM+B(L,R)*OMEGA(L,R)
    ENDDO
  ENDDO
  L12=S_SUM*16.0D0/3.0D0
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION B(L,R) ! EQ 110
    ! NOT THAT B FUNCTION AUTOMATICALLY HAS THE VARIABLES FROM L12,
    ! WHICH, MAKES THE PROAGRAM NEAT
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    INTEGER :: I
    DOUBLE PRECISION, EXTERNAL:: FACT, DELTA
    DOUBLE PRECISION B_SUM

    B_SUM=0.0D0
    B=0.0D0
    !!! REMENBER THAT 1-DELTA=0 CAUSE ZERO VALUE
    IF (R .EQ. P+Q+3) RETURN
    DO I=L-2,MIN(P,Q,R,(P+Q+2-R))
      !!! REMENBER THAT 1-DELTA(I,-1)=0 CAUSE ZERO VALUE
      IF (I .EQ. -1) THEN
        CYCLE
      END IF
      B_SUM=B_SUM+2.0**DBLE(2*R)/(4.0**DBLE(P+Q+2)) &
        &* 8.0**DBLE(I)*FACT(P+Q-2*I)/(FACT(P-I)*FACT(Q-I) ) &
        &*(-1.0)**DBLE(R+I)*(1.0-DELTA(I,-1)) / ( FACT(R-I)*(FACT(P+Q+2-I-R)) ) &
        &* FACT(R+1)/FACT(2*R+2) &
        &* FACT(2*(P+Q+3-I))/FACT(P+Q+3-I) &
        &* (-1.0)**DBLE(L)/(FACT(L) * FACT(I+2-L))&
        &* ( DBLE((I+1-L)*(I+2-L))*(DBLE(P+Q+1-I-R)*DBLE(P+Q+2-I-R)-0.5*DBLE((R-I)*(R-I-1)))&
        &    +1.5*DBLE((L-1)*L*(R-I)*(R-I-1)) - 2.0*DBLE(L*(I+2-L)*(R-I)*(P+Q+2-I-R))  )
    ENDDO

    B=B_SUM*M2**(P+1)*M1**(Q+1)
    RETURN
  END FUNCTION B
END FUNCTION L12


DOUBLE PRECISION FUNCTION L1(P,Q,I1,I2,OMEGA)  ! EQ. 111
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  INTEGER :: L,R
  DOUBLE PRECISION G,F, M0,M1,M2, S_SUM
  DOUBLE PRECISION :: MASS(2)
  COMMON /GASINFO/  MASS
  M0=I2 ! SUPPRESS ERROR
  ! NOTE HERE THAT ...
  ! WE ALWAYS HAVE M0 THE SUM OF ATOMIC MASSES
  ! M1 FOR THE ATOM "TREATED"
  ! AND M2 THE OTHER ATOM
  M0=MASS(1)+MASS(2) 
  M1=MASS(I1)/M0
  M2=MASS(3-I1)/M0 ! A TEMP SOLUTION FOR BIANRY MIXTURE
  G=(M1-M2)/M2
  F=(M1**2+M2**2)/(2*M1*M2)
  ! WRITE(*,*) ">>>L1<<<, M0, M1, M2, G, F"
  ! WRITE(*,*) "        ", M0, M1, M2, G, F
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=1,MIN(P,Q)+2
    DO R=L,(P+Q+4-L)
      ! WRITE(*,*) "B'*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      !!! EXTERNAL OMEGA IS MISSING
      S_SUM=S_SUM+B(L,R)*OMEGA(L,R)
      !S_SUM=S_SUM+B
    ENDDO
  ENDDO
  L1=S_SUM*16.0D0/3.0D0
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION B(L,R) ! EQ. 112
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    INTEGER I,W
    DOUBLE PRECISION B_SUM,B_SUM1,B_SUM2
    DOUBLE PRECISION, EXTERNAL :: DELTA, FACT, POCH

    B=0.0D0
    B_SUM=0.0D0
    B_SUM1=0.0D0
    !!! REMENBER THAT 1-DELTA=0 CAUSE ZERO VALUE
    IF (R .EQ. P+Q+3) RETURN
    DO I=L-2,MIN(P,Q,R,(P+Q+2-R))
      !!! REMENBER THAT 1-DELTA(I,-1)=0 CAUSE ZERO VALUE
      IF (I .EQ. -1) THEN
        CYCLE
      END IF
      B_SUM1= 2.0**DBLE(2*R)/4.0**DBLE(P+Q+2)&
        &*8.0**DBLE(I)*FACT(P+Q-2*I)/(FACT(P-I)*FACT(Q-I))&
        &*(-1.0)**DBLE(R+I)*FACT(R+1)&
        &/(FACT(R-I)*FACT(P+Q+2-I-R)*FACT(2*R+2))&
        &* FACT(2*(P+Q+3-I))/FACT(P+Q+3-I)
      B_SUM2=0.0D0
      DO W=0,MIN(P,Q,P+Q+2-R)-I
        B_SUM2=B_SUM2+POCH(P+1-I-W,W)*POCH(Q+1-I-W,W)&
          &/(FACT(W)*POCH(P+Q+1-2*I-W,W))&
          &*POCH(P+Q+3-I-R-W,W)/POCH(2*(P+Q+3-I)-2*W+1,W)&
          &*2.0**DBLE(2*W-2)*G**DBLE(W)&
          &*POCH(P+Q+4-I-W,W)/POCH(2*(P+Q+3-I)-W+1,W)&
          &*M1**(I)*M2**(I)*M2**(P+Q-2*I-W)&
          &*F**DBLE(I+2-L)*M1**(2)/(FACT(L)*FACT(I+2-L))&
          &*4.0*(&
          &1.5*M2**(2)/M1**(2)*DBLE(L*(L-1)*(R-I)*(R-I-1)) &
          &-2.0/F*M2**(1)/M1**(1)*DBLE(L*(I+2-L)*(R-I)*(P+Q+2-I-R-W))&
          &+1.0/F**2.0*DBLE((I+1-L)*(I+2-L))*( DBLE(P+Q+1-I-R-W)&
          &*DBLE(P+Q+2-I-R-W) - 0.5*M2**(2)/M1**(2)*DBLE((R-I)*(R-I-1)))&
          &)
      ENDDO
      B_SUM=B_SUM+B_SUM1*B_SUM2
    ENDDO

    B=B_SUM
    RETURN 
  END FUNCTION B
END FUNCTION


DOUBLE PRECISION FUNCTION LSG(P,Q,I1,I2,OMEGA) ! EQ 113
  IMPLICIT NONE
  ! INTEGER, PARAMETER :: MAXORD=20
  INTEGER, INTENT(IN) :: P,Q,I1,I2
  DOUBLE PRECISION, INTENT(IN) :: OMEGA(MAXORD,MAXORD)
  INTEGER :: L,R
  DOUBLE PRECISION S_SUM
  IF (I1.NE.I2) THEN
    WRITE(*,*) "HERE"
    STOP
  ENDIF
  S_SUM=0.0D0
  ! WRITE(*,*) "P, Q = ", P,Q
  DO L=2,MIN(P,Q)+2    ! L=1, MIN(P,Q)+2
    DO R=L,(P+Q+4-L)
      ! WRITE(*,*) "B'''*16/3=",B(L,R)*16.0/3.0,"L=",L,"R=",R
      !!! EXTERNAL OMEGA IS MISSING
      S_SUM=S_SUM+B(L,R)*OMEGA(L,R)
      !S_SUM=S_SUM+B
    ENDDO
  ENDDO
  LSG=S_SUM*16.0D0/3.0D0
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION B(L,R) !EQ 114
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: L,R
    DOUBLE PRECISION, EXTERNAL :: FACT, POCH, DELTA
    INTEGER I
    DOUBLE PRECISION B_SUM

    B=0.0D0
    B_SUM=0.0D0
    !IF (DELTA(R,P+Q+3)==1.0D0) RETURN
    DO I=L-2,MIN(P,Q,R,(P+Q+2-R))
      !!! REMENBER THAT 1-DELTA(I,-1)=0 CAUSE ZERO VALUE
      IF (I .EQ. -1) THEN
        CYCLE
      END IF
      B_SUM=B_SUM+(-1.0)**DBLE(R+I)/FACT(P+Q+2-I-R)&
        &*FACT(P+Q-2*I)&
        &/(FACT(P-I)*FACT(Q-I)*FACT(R-I))&
        &*FACT(2*(P+Q+3-I))/FACT(P+Q+3-I)&
        &*8.0**DBLE(I)/FACT(I+2-L)&
        &*( DBLE((I+1-L)*(I+2-L))&
        &*( DBLE((P+Q+1-I-R)*(P+Q+2-I-R))-0.5*DBLE((R-I)*(R-I-1)))&
        &+1.5*DBLE((L-1)*L*(R-I)*(R-I-1))-DBLE(2*L*(I+2-L)*(R-I)*(P+Q+2-I-R))&
        &)
    ENDDO

    B=B_SUM*0.5**DBLE(P+Q+2)*2.0**DBLE(2*R)/4.0**DBLE(P+Q+2)&
      &*FACT(R+1)/FACT(2*R+2)&
      &*(1.0+(-1.0)**DBLE(L))/FACT(L)
  END FUNCTION B

END FUNCTION

DOUBLE PRECISION FUNCTION B(P,Q,X1,X2) ! FOR EQ 5
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: P, Q
  DOUBLE PRECISION, INTENT(IN) :: X1, X2
  B=BB(P,Q)
  ! IF (P.LT.Q) B=BB(Q,P)
  ! WRITE(*,*) BB(P,Q), BB(-Q,-P), "KOKO"
  RETURN
CONTAINS
  DOUBLE PRECISION FUNCTION BB(P,Q) ! EQS. 7 8 9 10 
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: P, Q
    DOUBLE PRECISION, EXTERNAL :: BRACKETINTL
    INTEGER :: AP, AQ
    ! INTEGER, PARAMETER :: MAXORD=20
    DOUBLE PRECISION :: GOMEGA (MAXORD,MAXORD,2,2)
    DOUBLE PRECISION :: LINT(0:MAXORD, 0:MAXORD, 2, 2, 2, 2)
    COMMON /OMG/ GOMEGA,LINT
    BB=0.D0
    AP=ABS(P)
    AQ=ABS(Q)
    IF (P.GT.0 .AND. Q.GT.0) THEN
      BB=X1**2*LINT(AP-1,AQ-1,1,1,1,1) &
        & +X1*X2*LINT(AP-1,AQ-1,1,1,1,2)
    ELSEIF (P.GT.0 .AND. Q.LT.0) THEN
      BB=X1*X2*LINT(AP-1,AQ-1,1,2,1,2)
    ELSEIF (P.LT.0 .AND. Q.GT.0) THEN
      BB=X1*X2*LINT(AP-1,AQ-1,2,1,2,1)
    ELSEIF (P.LT.0 .AND. Q.LT.0) THEN
      BB=X2**2*LINT(AP-1,AQ-1,2,2,2,2)&
        &+X1*X2*LINT(AP-1,AQ-1,2,2,2,1)
    ENDIF
    RETURN
  END FUNCTION BB
END FUNCTION B


DOUBLE PRECISION FUNCTION FACT(N)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: N
  FACT=GAMMA(N+1.0D0)
  RETURN
END FUNCTION FACT

DOUBLE PRECISION FUNCTION POCH(Z,N) ! EQ.93
  IMPLICIT NONE
  INTEGER N,Z
  POCH=GAMMA(Z+N*1.0D0)/GAMMA(Z*1.0D0)
  RETURN
END FUNCTION POCH

DOUBLE PRECISION FUNCTION DELTA(I,J)
  IMPLICIT NONE
  INTEGER I,J
  IF (I .EQ. J) THEN
    DELTA=1.0D0
    RETURN
  ELSE
    DELTA=0.0D0
    RETURN
  END IF
  RETURN
END FUNCTION DELTA
! ADDITION FUNCTION



